[COVARIATE]
input = wt

EQUATION:
twt = log(wt/70)

[INDIVIDUAL]
input = {Tlag_pop, omega_Tlag, ka_pop, omega_ka, V_pop, beta_V_twt, twt, omega_V, Cl_pop, beta_Cl_twt, omega_Cl, ke0_pop, omega_ke0, th1_pop, omega_th1, th2_pop, th3_pop}

DEFINITION:
Tlag = {distribution=lognormal, typical=Tlag_pop, sd=omega_Tlag}
ka = {distribution=lognormal, typical=ka_pop, sd=omega_ka}
V = {distribution=lognormal, typical=V_pop, covariate=twt, coefficient=beta_V_twt, sd=omega_V}
Cl = {distribution=lognormal, typical=Cl_pop, covariate=twt, coefficient=beta_Cl_twt, sd=omega_Cl}
ke0 = {distribution=lognormal, typical=ke0_pop, sd=omega_ke0}
th1 = {distribution=normal, typical=th1_pop, sd=omega_th1}
th2 = {distribution=lognormal, typical=th2_pop, no-variability}
th3 = {distribution=lognormal, typical=th3_pop, no-variability}

[LONGITUDINAL]
DESCRIPTION: First order oral absorption with a lag-time, effect compartment, and ordered categorical data

input = {Tlag, ka, V, Cl, ke0, th1, th2, th3}

EQUATION:
{Cc,Ce}= pkmodel(Tlag,ka,V,Cl,ke0)

DEFINITION:
conc = {
	type=continuous, 	
	prediction=Cc, 
	errorModel=combined1
}
level = {
	type=categorical
	categories={1,2,3}
	logit(P(level<=1)) = -th1 + th2*Ce
	logit(P(level<=2)) = -th1 + th2*Ce + th3
}

OUTPUT:
output = {conc, level}

